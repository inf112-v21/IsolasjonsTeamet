plugins {
    id 'application'
    id 'java-library'
    id 'checkstyle'
    id 'org.beryx.runtime' version '1.12.1'
}

group = 'inf112.skeleton.app'
version = '0.2'

description = """isolasjonsteamet-roborally"""

application.mainClass = 'inf112.isolasjonsteamet.roborally.app.Main'


tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(15)
    }
}

// https://docs.gradle.org/current/userguide/toolchains.html#integration_with_tasks_relying_on_a_java_executable_or_java_home
def java15home = javaToolchains.compilerFor {
    languageVersion = JavaLanguageVersion.of(15)
}.get().metadata.installationPath.asFile.absolutePath

runtime {
    javaHome = java15home

    //Need these to not require JDK 15 for packaging
    //Remember to update thse now and then. JDK 15 is needed to update them
    modules = ['java.desktop', 'java.logging', 'jdk.unsupported']

    //Seems like compress 2 causes problems, so we'll avoid it
    options = ['--strip-debug', '--compress', '1', '--no-header-files', '--no-man-pages']

    jpackage {
        skipInstaller = true
        jpackageHome = java15home
    }

    launcher {
        jvmArgs = ['-Droborally.is-prod=true']
        noConsole = true
    }
}

checkstyle {
    configDirectory = rootProject.projectDir
    toolVersion = "8.40"
    ignoreFailures = false
    maxWarnings = 0
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    api "com.badlogicgames.gdx:gdx:$gdxVersion"
    api "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    api "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    api "com.google.guava:guava:30.1-jre"
    testImplementation(platform('org.junit:junit-bom:5.7.1'))
    testImplementation('org.junit.jupiter:junit-jupiter')
}
